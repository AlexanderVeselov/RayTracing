/*****************************************************************************
 MIT License

 Copyright(c) 2022 Alexander Veselov

 Permission is hereby granted, free of charge, to any person obtaining a copy
 of this softwareand associated documentation files(the "Software"), to deal
 in the Software without restriction, including without limitation the rights
 to use, copy, modify, merge, publish, distribute, sublicense, and /or sell
 copies of the Software, and to permit persons to whom the Software is
 furnished to do so, subject to the following conditions :

 The above copyright noticeand this permission notice shall be included in all
 copies or substantial portions of the Software.

 THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.IN NO EVENT SHALL THE
 AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 SOFTWARE.
 *****************************************************************************/

#extension GL_ARB_gpu_shader_int64 : enable
#extension GL_ARB_bindless_texture : enable

layout (local_size_x = 32) in;

#include "src/kernels/constants.h"
#include "src/kernels/shared_structures.h"
#include "src/kernels/sampling.h"

uniform uint bounce;
uniform uint width;
uniform SceneInfo scene_info;

layout(binding = 0, rgba32f) uniform image2D radiance_image;

layout(std140, binding = 0) uniform SampleCounter
{
    uint sample_counter;
};

layout(std140, binding = 1) uniform TextureHandles
{
    uint64_t texture_handles[MAX_TEXTURES];
};

layout(std430, binding = 0) buffer IncomingRays
{
    Ray incoming_rays[];
};

layout(std430, binding = 1) buffer IncomingRayCounter
{
    uint incoming_ray_counter[];
};

layout(std430, binding = 2) buffer IncomingPixelIndices
{
    uint incoming_pixel_indices[];
};

layout(std430, binding = 3) buffer OutgoingRays
{
    Ray outgoing_rays[];
};

layout(std430, binding = 4) buffer OutgoingRayCounter
{
    uint outgoing_ray_counter[];
};

layout(std430, binding = 5) buffer OutgoingPixelIndices
{
    uint outgoing_pixel_indices[];
};

layout(std430, binding = 6) buffer ShadowRays
{
    Ray shadow_rays[];
};

layout(std430, binding = 7) buffer ShadowRayCounter
{
    uint shadow_ray_counter[];
};

layout(std430, binding = 8) buffer ShadowPixelIndices
{
    uint shadow_pixel_indices[];
};

layout(std430, binding = 9) buffer DirectLightSamples
{
    float3 direct_light_samples[];
};

layout(std430, binding = 10) buffer Hits
{
    Hit hits[];
};

layout(std430, binding = 11) buffer Throughputs
{
    float3 throughputs[];
};

layout(std430, binding = 12) buffer Triangles
{
    Triangle triangles[];
};

layout(std430, binding = 13) buffer AnalyticLights
{
    Light analytic_lights[];
};

layout(std430, binding = 14) buffer EmissiveIndices
{
    uint emissive_indices[];
};

layout(std430, binding = 15) buffer Materials
{
    PackedMaterial materials[];
};

#include "src/kernels/material.h"
#include "src/kernels/light.h"

void main()
{
    uint incoming_ray_idx = gl_GlobalInvocationID.x;
    uint num_incoming_rays = incoming_ray_counter[0];

    if (incoming_ray_idx >= num_incoming_rays)
    {
        return;
    }

    Hit hit = hits[incoming_ray_idx];

    if (hit.primitive_id == INVALID_ID)
    {
        return;
    }

    Ray incoming_ray = incoming_rays[incoming_ray_idx];
    float3 incoming = -incoming_ray.direction.xyz;

    uint pixel_idx = incoming_pixel_indices[incoming_ray_idx];
    uint sample_idx = sample_counter;

    uint pixel_x = pixel_idx % width;
    uint pixel_y = pixel_idx / width;

    Triangle triangle = triangles[hit.primitive_id];

    float3 position = InterpolateAttributes(triangle.v1.position,
        triangle.v2.position, triangle.v3.position, hit.bc);

    float3 geometry_normal = normalize(cross(triangle.v2.position - triangle.v1.position,
        triangle.v3.position - triangle.v1.position));

    float2 texcoord = InterpolateAttributes2(triangle.v1.texcoord.xy,
        triangle.v2.texcoord.xy, triangle.v3.texcoord.xy, hit.bc);

    float3 normal = normalize(InterpolateAttributes(triangle.v1.normal,
        triangle.v2.normal, triangle.v3.normal, hit.bc));

    PackedMaterial packed_material = materials[triangle.mtlIndex];
    Material material;
    ApplyTextures(packed_material, material, texcoord);

    float3 hit_throughput = throughputs[pixel_idx];

#ifndef ENABLE_WHITE_FURNACE
    if (dot(material.emission.xyz, float3(1.0f, 1.0f, 1.0f)) > 0.0f)
    {
        vec4 radiance = imageLoad(radiance_image, ivec2(pixel_x, pixel_y));
        radiance.xyz += hit_throughput * material.emission.xyz;
        imageStore(radiance_image, ivec2(pixel_x, pixel_y), radiance);
    }
#endif // ENABLE_WHITE_FURNACE

    // Direct lighting
    {
        float s_light = SampleRandom(pixel_x, pixel_y, sample_idx, bounce, SAMPLE_TYPE_LIGHT);
        float3 outgoing;
        float pdf;
        float3 light_radiance = Light_Sample(scene_info, position, normal, s_light, outgoing, pdf);

        float distance_to_light = length(outgoing);
        outgoing = normalize(outgoing);

        float3 brdf = EvaluateMaterial(material, normal, incoming, outgoing);
        float3 light_sample = light_radiance * hit_throughput * brdf / pdf * max(dot(outgoing, normal), 0.0f);

        bool spawn_shadow_ray = (pdf > 0.0f) && (dot(light_sample, light_sample) > 0.0f);

        if (spawn_shadow_ray)
        {
            Ray shadow_ray;
            shadow_ray.origin.xyz = position + normal * EPS;
            shadow_ray.origin.w = 0.0f;
            shadow_ray.direction.xyz = outgoing;
            shadow_ray.direction.w = distance_to_light;

            ///@TODO: use LDS
            uint shadow_ray_idx = atomicAdd(shadow_ray_counter[0], 1);

            // Store to the memory
            shadow_rays[shadow_ray_idx] = shadow_ray;
            shadow_pixel_indices[shadow_ray_idx] = pixel_idx;
            direct_light_samples[shadow_ray_idx] = light_sample;
        }
    }

    // Indirect lighting
    {
        // Sample bxdf
        float2 s;
        s.x = SampleRandom(pixel_x, pixel_y, sample_idx, bounce, SAMPLE_TYPE_BXDF_U);
        s.y = SampleRandom(pixel_x, pixel_y, sample_idx, bounce, SAMPLE_TYPE_BXDF_V);
        float s1 = SampleRandom(pixel_x, pixel_y, sample_idx, bounce, SAMPLE_TYPE_BXDF_LAYER);

        float pdf = 0.0f;
        float3 throughput = to_float3(0.0f);
        float3 outgoing;
        float offset;
        float3 bxdf = SampleBxdf(s1, s, material, normal, incoming, outgoing, pdf, offset);

        if (pdf > 0.0)
        {
            throughput = bxdf / pdf;
        }

        throughputs[pixel_idx] *= throughput;

        bool spawn_outgoing_ray = (pdf > 0.0f);

        if (spawn_outgoing_ray)
        {
            ///@TODO: use LDS
            uint outgoing_ray_idx = atomicAdd(outgoing_ray_counter[0], 1);

            Ray outgoing_ray;
            outgoing_ray.origin.xyz = position + geometry_normal * EPS * offset;
            outgoing_ray.origin.w = 0.0f;
            outgoing_ray.direction.xyz = outgoing;
            outgoing_ray.direction.w = MAX_RENDER_DIST;

            outgoing_rays[outgoing_ray_idx] = outgoing_ray;
            outgoing_pixel_indices[outgoing_ray_idx] = pixel_idx;
        }
    }

}
