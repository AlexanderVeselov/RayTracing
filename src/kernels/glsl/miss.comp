/*****************************************************************************
 MIT License

 Copyright(c) 2022 Alexander Veselov

 Permission is hereby granted, free of charge, to any person obtaining a copy
 of this softwareand associated documentation files(the "Software"), to deal
 in the Software without restriction, including without limitation the rights
 to use, copy, modify, merge, publish, distribute, sublicense, and /or sell
 copies of the Software, and to permit persons to whom the Software is
 furnished to do so, subject to the following conditions :

 The above copyright noticeand this permission notice shall be included in all
 copies or substantial portions of the Software.

 THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.IN NO EVENT SHALL THE
 AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 SOFTWARE.
 *****************************************************************************/

layout (local_size_x = 32) in;

#include "src/kernels/shared_structures.h"
#include "src/kernels/constants.h"

uniform uint width;

layout(binding = 0) uniform sampler2D env_texture;

layout(std430, binding = 1) buffer Rays
{
    Ray rays[];
};

layout(std430, binding = 2) buffer RayCounter
{
    uint ray_counter[];
};

layout(std430, binding = 3) buffer Hits
{
    Hit hits[];
};

layout(std430, binding = 4) buffer PixelIndices
{
    uint pixel_indices[];
};

layout(std430, binding = 5) buffer Throughputs
{
    vec3 throughputs[];
};

layout(binding = 0, rgba32f) uniform image2D radiance_image;

float atan2(in float y, in float x)
{
    bool s = (abs(x) > abs(y));
    return mix(PI/2.0 - atan(x,y), atan(y,x), s);
}

float3 SampleSky(float3 dir)
{
    // Convert (normalized) dir to spherical coordinates.
    float2 coords = float2(atan2(dir.x, dir.y) + PI, acos(dir.z));
    coords.x = coords.x < 0.0f ? coords.x + TWO_PI : coords.x;
    coords.x *= INV_TWO_PI;
    coords.y *= INV_PI;

    return textureLod(env_texture, coords, 0.0f).xyz * 10.0f;
}

void main()
{
    uint ray_idx = gl_GlobalInvocationID.x;

    uint num_rays = ray_counter[0];

    if (ray_idx >= num_rays)
    {
        return;
    }

    Ray ray = rays[ray_idx];
    Hit hit = hits[ray_idx];

    if (hit.primitive_id == INVALID_ID)
    {
        uint pixel_idx = pixel_indices[ray_idx];
        uint pixel_x = pixel_idx % width;
        uint pixel_y = pixel_idx / width;

        float3 throughput = throughputs[pixel_idx];

#ifdef ENABLE_WHITE_FURNACE
        float3 sky_radiance = float3(0.5f, 0.5f, 0.5f);
#else
        float3 sky_radiance = SampleSky(ray.direction.xyz);
#endif
        vec4 radiance = imageLoad(radiance_image, ivec2(pixel_x, pixel_y));
        radiance.xyz += sky_radiance * throughput;
        imageStore(radiance_image, ivec2(pixel_x, pixel_y), radiance);
    }
}
